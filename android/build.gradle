allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
    configurations.configureEach {
        resolutionStrategy {
            force 'pl.droidsonroids.gif:android-gif-drawable:1.2.25'
        }
    }
}

subprojects { subproject ->
    subproject.plugins.withId("com.android.library") {
        subproject.android {
            if (namespace == null) {
                // Map specific packages that need custom namespaces
                def namespaceMap = [
                    'flutter_paypal_native': 'com.piccmaq.flutter_paypal_native',
                    'geolocator_android': 'com.baseflow.geolocator',
                ]
                if (namespaceMap.containsKey(subproject.name)) {
                    namespace = namespaceMap[subproject.name]
                } else {
                    namespace = "com.example.${subproject.name.replace('-', '_')}"
                }
            }
            if (!subproject.android.hasProperty('compileSdkVersion') || subproject.android.compileSdkVersion == null) {
                compileSdk 35
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }
}

// Configure Kotlin JVM target for all projects
allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '17'
        }
    }
}

// Provide flutter extension for plugins that expect it
ext {
    flutter = [
        compileSdkVersion: 35,
        minSdkVersion: 23,
        targetSdkVersion: 35
    ]
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
